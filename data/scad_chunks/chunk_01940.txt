module box_shelf_bracket(type, screw_positions, wall = undef) { //! Generates a shelf bracket, the first optional child is a 2D cutout and the second 3D cutouts, third child is 3D additions.
    w = is_undef(wall) ? box_wall(type) : wall;
    insert = box_shelf_insert(type);
    lip = 2 * insert_boss_radius(insert, w);
    width = max(insert_length(insert) + w, lip);

    module shape()
        difference() {
            square([box_width(type), box_depth(type)], center = true);

            offset(bezel_clearance / 2)
                box_corner_quadrants(type, box_width(type), box_depth(type));

            if($children)
                hflip()
                    children();
        }

    module boss()
        translate_z(-width + eps)
            linear_extrude(width - 2 * eps)
                hull() {
                    circle4n(r = lip / 2 - eps);

                    translate([-lip / 2, -lip / 2 + eps])
                        square([lip, eps]);
                }

    stl(box_bc_name(type, "shelf_bracket"))
        difference() {
            union() {
                linear_extrude(w)
                    difference() {
                        shape()
                            if($children)
                                children(0);

                        round(2) offset(-width)
                            shape()
                                if($children)
                                    children(0);
                    }

                linear_extrude(lip)
                    difference() {
                        shape()
                            if($children)
                                children(0);

                        offset(-w)
                            shape()
                                if($children)
                                    children(0);
                    }

                hflip() {
                    box_shelf_screw_positions(type, screw_positions, 0, w)
                        boss();

                    if($children > 2)
                        children(2);
                }
            }
            if($children > 1)
                hflip()
                    children(1);

            hflip()
                box_shelf_screw_positions(type, screw_positions, 0, w)
                    insert_hole(insert, counterbore = 1, horizontal = $horizontal);
        }
}