function cubic_real_roots(a, b, c, d) = //! Returns real roots of cubic equation
    let(b = b / a,
        c = c / a,
        d = d / a,
        inflection = -b / 3,
        p = c - b^2 / 3,
        q = 2 * b^3 / 27 - b * c / 3 + d,
        det = q^2 / 4 + p^3 / 27,
        roots = !p && !q ? 1 : nearly_zero(det) ? 2 :  det < 0 ? 3 : 1,
        r = sqrt(det),
        x = cuberoot(-q / 2 - r) + cuberoot(-q / 2 + r)
    ) roots == 1 ? [x] :
      roots == 2 ? [3 * q /p + inflection, -3 * q / p / 2 + inflection] :
      [for(i = [0 : roots - 1]) 2 * sqrt(-p / 3) * cos(acos(3 * q * sqrt(-3 / p) / p / 2) - i * 120) + inflection];