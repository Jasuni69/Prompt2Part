module grill_hole_positions(width, height, r = 1000) {
    nx = floor(width / (grill_hole + grill_gap));
    xpitch = width / nx;
    ny = floor(height / ((grill_hole + grill_gap) * cos(30)));
    ypitch = height / ny;

    for(y = [0 : ny - 1], x = [0 : nx - 1 - (y % 2)]) {
        $x = -width / 2 + (x + 0.5 + (y % 2) / 2) * xpitch;
        $y = -height / 2 + (y + 0.5) * ypitch;
        if(sqrt(sqr($x) + sqr($y)) + grill_hole / 2 <= r)
            translate([$x, $y])
                children();
    }
}