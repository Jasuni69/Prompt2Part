module base_insert(type) {
    length = insert_length(type);
    ring1_h = insert_ring1_h(type);

    chamfer1 = (insert_ring2_d(type) - insert_barrel_d(type)) / 2;
    chamfer2 = (insert_ring3_d(type) - insert_barrel_d(type)) / 2;
    ring2_h = ring1_h + chamfer1;
    gap = (length - ring1_h - ring2_h - chamfer2) / 3;

    $fn = 64;
    thread_d = insert_screw_diameter(type);
    explode(20, offset = [0, 0, -length]) translate_z(eps) vflip() {
        r1 = thread_d / 2;
        r2 = insert_barrel_d(type) / 2;
        r3 = insert_ring3_d(type) / 2;
        r4 = insert_ring2_d(type) / 2;
        r5 = insert_outer_d(type) / 2;
        h1 = ring1_h;
        h2 = ring1_h + gap;
        h3 = ring1_h + gap + ring2_h;
        h4 = ring1_h + gap + ring2_h + gap;
        color(brass)
            rotate_extrude(convexity = 3)
                polygon([
                    [r1, 0],
                    [r1, length],
                    [r2, length],
                    [r3, length - chamfer2],
                    [r3, h4],
                    [r2, h4],
                    [r2, h3],
                    [r4, h3 - chamfer1],
                    [r4, h2],
                    [r2, h2],
                    [r2, h1],
                    [r5, h1],
                    [r5, 0],
                ]);

        if(show_threads)
            female_metric_thread(thread_d, metric_coarse_pitch(thread_d), length, center = false, colour = brass);
    }
}