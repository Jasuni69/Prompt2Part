module phillips_drive(size="#2", shaft=6, l=20, orient=ORIENT_Z, align=V_UP) {
	// These are my best guess reverse-engineered measurements of
	// the tip diameters of various phillips screwdriver sizes.
	ang = 11;
	rads = [["#1", 1.25], ["#2", 1.77], ["#3", 2.65]];
	radidx = search([size], rads)[0];
	r = radidx == []? 0 : rads[radidx][1];
	h = (r/2)/tan(ang);
	cr = r/2;
	orient_and_align([shaft, shaft, l], orient, align) {
		down(l/2) {
			difference() {
				intersection() {
					union() {
						clip = (shaft-1.2*r)/2/tan(26.5);
						zrot(360/8/2) cylinder(h=clip, d1=1.2*r/cos(360/8/2), d2=shaft/cos(360/8/2), center=false, $fn=8);
						up(clip-0.01) cylinder(h=l-clip, d=shaft, center=false, $fn=24);
					}
					cylinder(d=shaft, h=l, center=false, $fn=24);
				}
				zrot(45)
				zring(n=4) {
					yrot(ang) {
						zrot(-45) {
							off = (r/2-cr*(sqrt(2)-1))/sqrt(2);
							translate([off, off, 0]) {
								linear_extrude(height=l*2, convexity=4) {
									difference() {
										union() {
											square([shaft, shaft], center=false);
											back(cr) zrot(1.125) square([shaft, shaft], center=false);
											right(cr) zrot(-1.125) square([shaft, shaft], center=false);
										}
										difference() {
											square([cr*2, cr*2], center=true);
											translate([cr,cr,0]) circle(r=cr, $fn=8);
										}
									}
								}
							}
						}
					}
				}
			}
		}
	}
}


// vim: noexpandtab tabstop=4 shiftwidth=4 softtabstop=4 nowrap