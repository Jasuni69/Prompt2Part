module threaded_insert(type) { //! Draw specified threaded insert, for use in wood
    d2 = insert_outer_d(type);
    d3 = insert_barrel_d(type);
    pitch = threaded_insert_pitch(type);

    profile = thread_profile((d2 - d3) /2 , 0, 60);
    socket = insert_screw_diameter(type) / cos(30) / 2;
    length = insert_length(type);

    r=insert_barrel_d(type) / 2;
    z=threaded_insert_chamfer(type);

    thread_l = insert_length(type) - z; // - insert_ring1_h(type);

    vitamin(str("threaded_insert(", type[0], "): Threaded insert M", insert_screw_diameter(type), " x ", length, "mm"));
    union() {
        color(silver)
            render() difference() {
                base_insert(type);
                translate_z(-socket/2 + 0.01)
                    cylinder(r=socket, $fn = 6, h=socket/2);

                // chamfer the end
                rotate_extrude(convexity = 3)
                    polygon([
                        [r - z, -length],
                        [r + 0.1, - length],
                        [r + 0.1, - length + z + 0.1]
                    ]);
            }

        translate_z(-thread_l/2)
            thread(insert_barrel_d(type),
                pitch,
                thread_l,
                profile,
                center = true,
                top = 1,
                bot = -1,
                starts = 1,
                solid = false,
                female = false,
                colour = silver);
    }
}