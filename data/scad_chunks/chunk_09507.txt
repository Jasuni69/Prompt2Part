function gear_tooth_profile(
	mm_per_tooth    = 3,     //this is the "circular pitch", the circumference of the pitch circle divided by the number of teeth
	number_of_teeth = 11,    //total number of teeth around the entire perimeter
	pressure_angle  = 28,    //Controls how straight or bulged the tooth sides are. In degrees.
	backlash        = 0.0,   //gap between two meshing teeth, in the direction along the circumference of the pitch circle
	bevelang        = 0.0,   //Gear face angle for bevelled gears.
	clearance       = undef, //gap between top of a tooth on one gear and bottom of valley on a meshing gear (in millimeters)
	interior        = false,
	valleys         = true
) = let(
		p = pitch_radius(mm_per_tooth, number_of_teeth),
		c = outer_radius(mm_per_tooth, number_of_teeth, clearance, interior),
		r = root_radius(mm_per_tooth, number_of_teeth, clearance, interior),
		b = base_radius(mm_per_tooth, number_of_teeth, pressure_angle),
		t  = mm_per_tooth/2-backlash/2,               //tooth thickness at pitch circle
		k  = -_gear_iang(b, p) - t/2/p/PI*180,              //angle to where involute meets base circle on each side of tooth
		mat = matrix3_scale([1,1/cos(bevelang), 1]) * matrix3_translate([0,-r,0]),
		points=[
			if(valleys) _gear_polar(r, -181/number_of_teeth),
			_gear_polar(r, r<b ? k : -180/number_of_teeth),
			_gear_q7(0/5,r,b,c,k, 1), _gear_q7(1/5,r,b,c,k, 1), _gear_q7(2/5,r,b,c,k, 1), _gear_q7(3/5,r,b,c,k, 1), _gear_q7(4/5,r,b,c,k, 1), _gear_q7(5/5,r,b,c,k, 1),
			_gear_q7(5/5,r,b,c,k,-1), _gear_q7(4/5,r,b,c,k,-1), _gear_q7(3/5,r,b,c,k,-1), _gear_q7(2/5,r,b,c,k,-1), _gear_q7(1/5,r,b,c,k,-1), _gear_q7(0/5,r,b,c,k,-1),
			_gear_polar(r, r<b ? -k : 180/number_of_teeth),
			if(valleys) _gear_polar(r, 181/number_of_teeth),
		]
	) matrix3_apply(points, [mat]);