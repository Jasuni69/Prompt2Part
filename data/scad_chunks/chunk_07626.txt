module Pfeil(l=[+2,+3.5],b=+2,shift=0,grad=60,d=0,center=true,name,help){
 shift=is_list(shift)?shift:[shift,-shift];
 l=is_list(l)?l:[l/2,l/2]; 
 b=is_list(b)?b:[b,2*(l[1]-(d?0:shift[0]))*tan(grad/2)];
 center=is_bool(center)?center?[1,1]:[0,0]:is_list(center)?center:[center,center];
 d=d?max(abs(d),abs(b[1]))*sign(d):0;
 gradB=d?gradB(b=l[1]+ shift[1],r=d/2)  :0; // l√§nge Pfeilspitze auf Kreis
 
 fnD=max(8,ceil(norm([b[1]/2,l[1]])/$fs)); // fraqments gebogene Spitze
 fnDend=max(10,ceil(abs(l[0])/$fs));
 
 spitze=false; // gebogene Spitze = false
 
 points=[
     [l[1],0],//spitze
     [shift[0],b[1]/2],
     [0,b[0]/2],   
  if(!d)[-l[0],b[0]/2],//End oben
  if(!d)[-l[0]+shift[1],0],//End mitte
  if(!d)[-l[0],-b[0]/2],//End unten
     [0,-b[0]/2],   
     [shift[0],-b[1]/2],   
    ];   


  pointsD=[
    [0,d/2-b[0]/2],
    each kreis(rand=0,grad=gradB(r=d/2+b[0]/2*0,b=-l[0]),d=d+b[0],center=0,fn=fnDend),
    [sin(-gradB(r=d/2,b=l[0] +shift[0]))*d/2,cos(-gradB(r=d/2,b=l[0] +shift[0]))*d/2],//shift End
    each kreis(rand=0,grad=-gradB(r=d/2-b[0]/2*0,b=-l[0]),d=d-b[0],center=0,rot=gradB(r=d/2-b[0]/2*0,b=-l[0]),fn=fnDend),
    
    [0,d/2+b[0]/2],
    for(i=[0:fnD ])   let(deg=i*gradB/fnD - gradB(shift[1],r=d/2),r=d/2 +(b[1]/2/fnD)*(fnD-i))
      [sin(deg)*r,cos(deg)*r],
    for(i=[fnD :-1:0])let(deg=i*gradB/fnD - gradB(shift[1],r=d/2),r=d/2 -(b[1]/2/fnD)*(fnD-i))
      [sin(deg)*r,cos(deg)*r],
      
    ];

  if(d)translate(center.y?center.y<0?[0,d/2]:
                                  [0,0]:
                  [0,-d/2]){
      if(spitze)union(){
      Kreis(d=d,rand=b[0],b=-l[0],center=false,rcenter=true,rot=-90); 
      T(y=d/2)polygon(points);
      }
      else polygon(pointsD);

  }
  else translate([center.x?center.x>0?0:-l[1]:l[0],center.y?center.y>0?0:-b[1]/2:b[1]/2]) polygon(points);  
    
InfoTxt("Pfeil",["Winkel",2*atan((b[1]/2)/(l[1]-shift[0]))],name);    
HelpTxt("Pfeil",[   
    "l",l,
    "b",b,
    "shift",shift,
    "grad",grad,
    "d",d,
    "center",center,
    "name",name],help);
}

/** \page Polygons
DPfeil() creates a double head arrow
\brief creates a double head arrow
\name DPfeil
\param l total length
\param b width  [tail,head] or teil and head is calculated by angle(grad)
\param shift shifting center and end points
\param grad arrow head angle
\param d form circular arrow
\param txt add text
\param center centers arrow
\param name  names arrow
\param help activate help

##Example
DPfeil(l=[60,10],grad=30,txt="ABC");  
T(0,10)DPfeil();  
T(0,-10)DPfeil(shift=-1,txt=true);
*/