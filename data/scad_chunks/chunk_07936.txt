module text_tower(tx, font_name, radius, height, thickness) {

    font_size = 2 * PI * radius / $fn;
    angle_step = 360 / $fn;
    half_angle_step = angle_step / 2;
    half_thickness = thickness / 2;

    module body() {
        points = helix(
            radius = radius, 
            levels = height / font_size, 
            level_dist = font_size, 
            vt_dir = "SPI_DOWN"
        );
        for(i = [0:len(points) - 1]) {
            translate(points[i])
            rotate([90, 0, 90 + angle_step * i]) 
            linear_extrude(thickness, center = true) 
                text(
                    tx[i], 
                    font = font_name, 
                    size = font_size, 
                    halign = "center"
                );
        }

        rotate(-half_angle_step) 
            helix_extrude(
                [
                    [thickness, half_thickness],
                    [-thickness, half_thickness], 
                    [-thickness, -half_thickness],
                    [thickness, -half_thickness]
                ], 
                radius = radius, 
                levels = height / font_size + 1, 
                level_dist = font_size,
                vt_dir = "SPI_DOWN"
            );
    }

    translate([0, 0, -font_size - half_thickness]) 
        body();

    rotate(-half_angle_step) 
    translate([0, 0, -font_size - thickness]) 
        cylinder(h = font_size, r = radius + thickness);
}

text_tower(tx, font_name, radius, height, thickness, $fn = 24);