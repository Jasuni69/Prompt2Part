module wall(radius, height, thickness) {
    bk_number = 8;
    half_thickness = thickness / 2;
    ro = radius;
    ri = ro * 0.541196;
    leng = ro / 2.3;
    
    bk_w = leng / bk_number / 2;   
    module bk() {
        for(i = [0:bk_number + 2]) {
            translate([(2 * i + 1) * bk_w, 0]) 
                square([bk_w, bk_w * 2], center = true);
        }
    }    

    module eight_pts_star(r, th) {    
        hollow_out(shell_thickness = th) 
            intersection() {
                rotate(22.5) 
                    polygon(shape_star(ro, ri, 8)); 
                circle(ro * 0.9);
            }
    }

    linear_extrude(height) 
        eight_pts_star(ro - thickness, thickness);

    translate([0, 0, height]) 
    linear_extrude(half_thickness) 
    difference() {
        eight_pts_star(ro - thickness, thickness / 4);
        
        union() {
            for(i = [0:7]) {
                rotate(22.5 + i * 45) 
                translate([-ro, 0]) 
                rotate(-22.5) {
                    bk();
                    rotate(45) bk();
                }
            }
        }
    }
}