module foot_assembly(t = 0, type = foot, flip = false, no_washer = false) { //! Assembly with fasteners in place for specified sheet thickness
    screw = foot_screw(type);
    nut = screw_nut(screw);
    squeeze = 0.5;
    screw_length = screw_length(screw, foot_thickness(type) + t - squeeze, no_washer ? 1 : 2, nyloc = true);

    vflip() explode(15, true) {
        translate_z(-squeeze)
            stl_colour(pp4_colour) foot(type);

        if(t)
            explode(15, true)
                translate_z(foot_thickness(type) - squeeze)
                    if(flip)
                        nut_and_washer(nut, true);
                    else
                        screw_and_washer(screw, screw_length);
    }
    if(t)
        translate_z(t)
            if(flip)
                if(no_washer)
                    screw(screw, screw_length);
                else
                    screw_and_washer(screw, screw_length);
            else
                if(no_washer)
                    nut(nut, true);
                else
                    nut_and_washer(nut, true);
}