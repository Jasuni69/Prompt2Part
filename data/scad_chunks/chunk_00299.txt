module d_plug(type, socket = false, pcb = false, idc = false) { //! Draw specified D plug, which can be IDC, PCB or plain solder bucket
    hole_r = 3.05 / 2;
    dwall = 0.5;
    $fa = fa; $fs = fs;

    flange_length    = d_flange_length(type);
    d_length         = d_lengths(type)[socket ? 1 : 0];
    hole_pitch       = d_hole_pitch(type);
    d_width          = d_widths(type)[socket ? 1 : 0];
    flange_width     = d_flange_width(type);
    front_height     = d_front_height(type);
    back_height      = d_height(type) - front_height;
    pins             = d_ways(type);

    desc = idc ? "IDC" : pcb ? "PCB mount" : "";
    vitamin(str(socket ? "d_socket(" : "d_plug(", type[0], arg(pcb, false, "pcb"), arg(idc, false, "idc"),
                                  "): D-type ", pins, " way ", desc, socket ? " socket" : " plug"));

    module pin_positions()
        for($i = [1 : pins])
            translate([($i - (pins + 1) / 2) * 2.77 / 2, ($i % 2 - 0.5) * 2.84])
                children();
    //
    // Shell
    //
    color(d_plug_shell_colour)  {
        linear_extrude(d_flange_thickness(type))
            difference() {
                rounded_square([flange_length, flange_width], 2);

                d_connector_holes(type)
                    circle(hole_r);
            }

        linear_extrude(front_height, convexity = 5)
            difference() {
                d_plug_D(d_length, d_width, 2.5);
                d_plug_D(d_length - 2 * dwall, d_width - 2 * dwall, 2.5 - dwall);
            }

        if(!idc)
            hflip()
                linear_extrude(back_height, convexity = 5)
                    d_plug_D(d_lengths(type)[0] + 2 * dwall, d_widths(type)[0] + 2 * dwall, 2.5 + dwall);

    }
    //
    // Insulator
    //
    color(d_plug_insulator_colour) {
        translate_z(d_flange_thickness(type) + eps)
            rotate([0, 180, 0])
                linear_extrude(back_height + 1 + d_flange_thickness(type), convexity = 5)
                    d_plug_D(d_length - dwall, d_width - dwall, 2.5 - dwall/2);

        if(socket)
            linear_extrude(front_height - eps, convexity = 5)
                difference() {
                    d_plug_D(d_length - dwall, d_width - dwall, 2.5 - dwall/2);

                    pin_positions()
                        circle(r = 0.7);
                }
        if(idc) {
            translate_z(-2.4 / 2)
                cube([((pins + 1) / 2) * 2.77 + 6, flange_width, 2.4], center = true);

            translate_z(-14.4 / 2)
                cube([pins * 1.27 + 7.29, flange_width, 14.4], center = true);
        }
    }
    //
    // Pins
    //
    color("gold") {
        $fn = fn;
        if(!socket)
            translate_z(-0.5)
                pin_positions()
                    hull() {
                        pin_r = 0.5;
                        cylinder(r = pin_r, h = eps);

                        translate_z(front_height - pin_r)
                            sphere(pin_r);
                    }

        if(pcb)
           rotate([0, 180, 0]) {
                linear_extrude(back_height + 1 + 4.5)
                    pin_positions()
                         circle(r = 0.75 / 2);

                linear_extrude(back_height + 1 + 1)
                    pin_positions()
                         circle(r = 0.75);
            }

        if(!pcb && !idc)
            rotate([0, 180, 0])
                pin_positions()
                    rotate(180 + ($i % 2) * 180)
                    render() difference() {
                        linear_extrude(8)
                            difference() {
                                circle(1);

                                circle(0.45);
                            }
                        translate([0, 2.1, 8])
                            rotate([45, 0, 0])
                                cube([3, 3, 3], center = true);
                    }
    }
    if(pcb)
        translate_z(-d_pcb_offset(type))
            pin_positions()
                solder(0.75 / 2);
}