module BB(
r=10,
ball=5,
rand=1,
e,
spiel=0.125,
support=0.15,
h,
top,
cage=false,
cyl=true,
achse,
od,
rad=[.75,.75],
center=true,
name,
help    
){
$info=false;
$helpM=false;    
rand=is_list(rand)?rand:[rand,rand]; 
top=is_undef(top)?rand[0]/2:top;   
h=is_undef(h)?ball+top*2:h;
    
if(achse&&achse>r*2-ball-2*rand[1]-2*spiel)Echo("BB Achse zu groß - limited",color="red"); 
if(od&&od<r*2+ball+2*rand[0]+2*spiel)Echo("BB OD zu klein - limited",color="red");
if(h<ball) Echo("BB h kleiner Kugel",color="red");
if(h>ball+top*2) Echo("BB Kugel = Walze!",color="green");    
achseDia=is_undef(achse)?r*2-ball-2*rand[1]-2*spiel:min(r*2-ball-2*rand[1]-2*spiel,achse);
oDia=is_undef(od)? r*2+ball+2*rand[0]+2*spiel:max(od,r*2+ball+2*rand[0]+2*spiel);  
e=is_undef(e)?floor(360/gradS(r=r,s=ball+spiel)):min(e,floor(360/gradS(r=r,s=ball+spiel)));    


  InfoTxt("BB",["Achse",achseDia,"Hoch",h,"OD",oDia,"Kugel∅",h>ball+top*2?str(ball,"×",h-top*2):ball,"Anzahl",e],name); 
 Tz(center?0:h/2){   
    Polar(e,r)if(h>ball+top*2)Pille(h-top*2,d=ball);else sphere(d=ball);
    difference(){
        union(){
            Pille(h,d=oDia,rad=rad[0]);//Body
            Tz(center?0:-h/2)children();
        }
      if(achseDia) Strebe(h,d=achseDia,rad=rad[1],center=true);// Achse    
        
        if(h>ball+top*2)Torus(trx=r,d=ball+spiel*2)MKlon(mx=1)Pille(h-top*2+spiel*2,d=$d,2D=+1);
            else Torus(trx=r,d=ball+spiel*2);//Rille
        Mklon(tz=h/2+1.25)Torus(trx=r,d=ball,fn2=6);//innenfase
        Ring(h+1,d=r*2,rand=ball/2,rcenter=true,center=true);//Trennspalt
    }
    
 if (cage) difference(){
    union(){
        if(h>ball+top*2)Torus(trx=r,d=ball-spiel*2)MKlon(mx=1)Pille(h-top*2-spiel*2,d=$d,2D=+1);
            else Torus(trx=r,d=ball-spiel*2);
        Ring(h=h,d=r*2,rand=ball/2,rcenter=true,center=true);
    }
    Polar(e,r)if(h>ball+top*2)Pille(h-top*2+.6,d=ball+.6); else sphere(d=ball+.6);
    Tz(-h/2) Ring(h=top,d=r*2,rand=ball,rcenter=true,center=false);    
 }  

//supportbrim
 if(support&&!cyl)difference(){
     Tz(-h/2) Ring(h=top+.2,d=r*2,rand=ball/2-.5,rcenter=true,center=false);
     
     if (h>ball+top*2) Tz(-h/2+top+ball/2)Polar(e,r)sphere(r=ball/2+support);
         else Polar(e,r)sphere(r=ball/2+support);
 }
 if(cyl)Tz(-h/2){
     Polar(e,r)cylinder(h=h/2,d=ball/2-.5);
     if(support)Ring(l(2),r=r,rand=n(2),rcenter=true);
 }
 }
HelpTxt("BB",["r",r,"ball",ball,"rand",rand,"e",e,"spiel",spiel,"support",support,"h",h,"top",top,"cage",cage,"cyl",cyl,"achse",achse,"od",od,"rad",rad,"center",center,"name",name],help); 
}

/** \page Product
* \name Pin
Pin() creates a pin or Peg to fix or swivel with 45° lip
\param l length ↦ [bottom,top]
\param d diameter of core body
\param cut number incisions [bottom,top]
\param mitte middle lip
\param lippe lip size (od=d+2×lip)
\param spiel lip height and clearance between connected parts
\param center center pin
*/