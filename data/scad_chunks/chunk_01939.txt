module box_shelf_screw_positions(type, screw_positions, thickness = 0, wall = undef, top_screws = true) { //! Place children at the shelf screw positions
    w = is_undef(wall) ? box_wall(type) : wall;
    insert = box_shelf_insert(type);
    translate_z(-insert_boss_radius(insert, w))
        for(p = screw_positions)
            multmatrix(p)
                translate_z(thickness)
                    let($horizontal = true)
                        children();

    r = box_boss_r(type);
    inset = box_intrusion(type) - r + (r + insert_boss_radius(insert, w) + bezel_clearance / 2) / sqrt(2);
    if(top_screws)
        translate_z(thickness)
            for(x = [-1, 1], y = [-1, 1])
                translate([x * (box_width(type) / 2 - inset), y * (box_depth(type) / 2 - inset)])
                    rotate(45 * x * (2 + y))
                        let($horizontal = false)
                            children();
}